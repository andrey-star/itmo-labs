start: s
accept: ac
reject: rj
blank: _

s 0 -> prepend= 0 <
s 1 -> prepend= 1 <
s 2 -> prepend= 2 <

prepend= _ -> prepend0 = <

prepend0 _ -> prepend0 0 >
prepend0 = -> mul = >


mul 0 -> mul 0 >
mul 1 -> mul 1 >
mul * -> sub1_or_return * >

sub1_or_return _ -> ret _ <
sub1_or_return 0 -> sub1_or_return 0 >
sub1_or_return 1 -> sub1 1 ^

sub1 _ -> first_not0 _ <
sub1 0 -> sub1 0 >
sub1 1 -> sub1 1 >

first_not0 0 -> first_not0 0 <
first_not0 1 -> replace_right_zeros_with_1 0 >

replace_right_zeros_with_1 _ -> go_left_to* _ <
replace_right_zeros_with_1 0 -> replace_right_zeros_with_1 1 >

go_left_to* 0 -> go_left_to* 0 <
go_left_to* 1 -> go_left_to* 1 <
go_left_to* * -> rightmost_digit_before* * <

go_right_to* * -> rightmost_digit_before* * <
go_right_to* 0 -> go_right_to* 0 >
go_right_to* 1 -> go_right_to* 1 >
go_right_to* = -> go_right_to* = >
go_right_to* 1' -> go_right_to* 1' >
go_right_to* 0' -> go_right_to* 0' >

rightmost_digit_before* 0 -> goto=_and_add0 0' <
rightmost_digit_before* 1 -> goto=_and_add1 1' <
rightmost_digit_before* = -> goto_*_and_clear'_before = ^
rightmost_digit_before* 0' -> rightmost_digit_before* 0' <
rightmost_digit_before* 1' -> rightmost_digit_before* 1' <

goto=_and_add0 0 -> goto=_and_add0 0 <
goto=_and_add0 1 -> goto=_and_add0 1 <
goto=_and_add0 = -> add0 = <

add0 _ -> go_right_to* 0' ^
add0 0 -> go_right_to* 0' ^
add0 1 -> go_right_to* 1' ^
add0 1' -> add0 1' <
add0 0' -> add0 0' <

goto=_and_add1 0 -> goto=_and_add1 0 <
goto=_and_add1 1 -> goto=_and_add1 1 <
goto=_and_add1 = -> add1 = <

add1 _ -> go_right_to* 1' ^
add1 0 -> go_right_to* 1' ^
add1 1 -> add_carry 0' <
add1 1' -> add1 1' <
add1 0' -> add1 0' <

add_carry _ -> go_right_to* 1 ^
add_carry 0 -> go_right_to* 1 ^
add_carry 1 -> add_carry 0 <

goto_*_and_clear'_before = -> goto_*_and_clear'_before = >
goto_*_and_clear'_before 0' -> goto_*_and_clear'_before 0' >
goto_*_and_clear'_before 1' -> goto_*_and_clear'_before 1' >
goto_*_and_clear'_before * -> clr'_before * <

clr'_before _ -> goto_after= _ >
clr'_before 0' -> clr'_before 0 <
clr'_before 1' -> clr'_before 1 <
clr'_before 0 -> clr'_before 0 <
clr'_before 1 -> clr'_before 1 <
clr'_before = -> clr'_before = <

goto_after= 0 -> goto_after= 0 >
goto_after= 1 -> goto_after= 1 >
goto_after= = -> mul = >

ret 0 -> ret _ <
ret 1 -> ret _ <
ret * -> ret _ <
ret = -> goto_first_left_blank _ <

goto_first_left_blank _ -> ac _ >
goto_first_left_blank 0 -> goto_first_left_blank 0 <
goto_first_left_blank 1 -> goto_first_left_blank 1 <
